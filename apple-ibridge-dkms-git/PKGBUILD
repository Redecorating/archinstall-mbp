# Copied from https://github.com/aunali1/apple-bce-arch/blob/master/PKGBUILD but this one is for apple-ibridge

_pkgbase=('apple-ibridge')
pkgname=(${_pkgbase}-git ${_pkgbase}-dkms-git)
pkgdesc="Apple ibridge (touchbar) driver for T2-based devices."
# number of commits & latest commit hash
pkgver=r305.fc9aefa
pkgrel=1

arch=('x86_64')
url="https://github.com/t2linux/apple-ib-drv"
license=('GPL2')

depends=('linux')
makedepends=('git' 'linux-headers')
options=(!strip)

source=("apple-ibridge::git+https://github.com/t2linux/apple-ib-drv.git#branch=mbp15"
	"dkms.conf.in"
	"rm-apple-ib-tb.sh")

sha256sums=('SKIP'
            'ae3dfeba1fcb569fb73b4c436dcf269b35340ec8c3d848b8f1cda5783e198faf'
            '5d9033923e784068d13116beed97ae8646052fe04732306149a10d6777b1cadb')

pkgver() {
	cd "$_pkgbase"
	( set -o pipefail
	  git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
	  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Set name and version
	sed -e "s/@PKGVER@/$pkgver/" \
	    -e "s/@_PKGBASE@/$_pkgbase/" dkms.conf.in > dkms.conf
}

build() {
	cd "$_pkgbase"
	_kernver=$(</usr/src/linux-mbp/version)
	make KVERSION=${_kernver}
}

package_apple-ibridge-git() {
	depends=('linux-mbp' 'linux-mbp-headers')

	_kernver=$(</usr/src/linux-mbp/version)
	_extramodules="/usr/lib/modules/${_kernver}/extramodules"

	install -Dm744 rm-apple-ib-tb.sh "${pkgdir}"/usr/lib/systemd/system-sleep/rm-apple-ib-tb.sh

	cd "$_pkgbase"
	install -Dt "${pkgdir}${_extramodules}" -m644 *.ko
	find "${pkgdir}" -name '*.ko' -exec xz {} +
}

package_apple-ibridge-dkms-git() {
	depends=('dkms')
	conflicts=("${_pkgbase}")
	provides=("${_pkgbase}")

	install -Dm644 dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf
	install -Dm744 rm-apple-ib-tb.sh "${pkgdir}"/usr/lib/systemd/system-sleep/rm-apple-ib-tb.sh
	
	cd "$_pkgbase"

	# XXX: Find a better solution...
	make KVERSION=$(</usr/src/linux-mbp/version) clean

	cp -dr --no-preserve='ownership' ./* "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/
}
